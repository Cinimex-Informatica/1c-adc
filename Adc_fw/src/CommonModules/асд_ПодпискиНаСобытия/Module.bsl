#Область СлужебныеПроцедурыИФункции

// Производит запуск менеджера АСД из общего модуля "асд_ОсновнойМодульСервер".
// 
// Параметры:
//   Источник - СправочникОбъект, ДокументОбъект - объект, вызвавший срабатывание подписки на событие.
//   Отказ - Булево - признак отказа от выполнения операции, если установить в "Истина", действие будет отменено.
//   ИсполняемаяКоманда - Строка - исполняемая команда, передаваемая менеджеру АСД.
//
// Пример:
//   асд_ЗапуститьМенеджерАСД(Источник, Отказ, "ОбработкаУдаленияПроведенияДокумента");
//
Процедура асд_ЗапуститьМенеджерАСД(Источник, Отказ, ИсполняемаяКоманда)
	
	Если НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() 
		И Константы.асд_ИспользоватьАСД.Получить()
	 	И НЕ (Отказ ИЛИ Источник.ОбменДанными.Загрузка)
	 	И асд_ОсновнойМодульСервер.ЕстьПравилаАСД(Источник) Тогда
		
		GUIDРодительскогоПотока = Неопределено;
		ОтменятьПроведениеПодчиненных = Неопределено;
		
		Источник.ДополнительныеСвойства.Свойство("GUIDРодительскогоПотока", GUIDРодительскогоПотока);
		Источник.ДополнительныеСвойства.Свойство("ОтменятьПроведениеПодчиненных", ОтменятьПроведениеПодчиненных);
		
		КлючТекущегоПотока = Новый УникальныйИдентификатор;
		
		ПараметрыПроцедуры = Новый Структура();
		ПараметрыПроцедуры.Вставить("ОбъектОснованиеСсылка",			Источник.Ссылка); 
		ПараметрыПроцедуры.Вставить("GUIDРодительскогоПотока",			GUIDРодительскогоПотока);
		ПараметрыПроцедуры.Вставить("КлючТекущегоПотока",				КлючТекущегоПотока); 
		ПараметрыПроцедуры.Вставить("ИсполняемаяКоманда",				ИсполняемаяКоманда); 
		ПараметрыПроцедуры.Вставить("ОтменятьПроведениеПодчиненных",	?(ОтменятьПроведениеПодчиненных = Неопределено, Истина, ОтменятьПроведениеПодчиненных));
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание объектов АСД'");
		ПараметрыВыполнения.ОжидатьЗавершение = 0;
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючТекущегоПотока;
		
		ДлительныеОперации.ВыполнитьВФоне("асд_ОсновнойМодульСервер.МенеджерАСД", ПараметрыПроцедуры, ПараметрыВыполнения);
			
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СобытияДокументов

Процедура асд_ОбработкаПроведенияДокумента(Источник, Отказ, РежимПроведения) Экспорт
	
	асд_ЗапуститьМенеджерАСД(Источник, Отказ, "ОбработкаПроведенияДокумента");
  
КонецПроцедуры

Процедура асд_ОбработкаУдаленияПроведенияДокумента(Источник, Отказ) Экспорт
	
	асд_ЗапуститьМенеджерАСД(Источник, Отказ, "ОбработкаУдаленияПроведенияДокумента");
	
КонецПроцедуры

#КонецОбласти 

#Область СобытияСправочников

Процедура асд_ПриЗаписиСправочника(Источник, Отказ) Экспорт
	
	асд_ЗапуститьМенеджерАСД(Источник, Отказ, "ПриЗаписиСправочника");
		
КонецПроцедуры

#КонецОбласти
