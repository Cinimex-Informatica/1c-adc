#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	//test git
	
	//Определим в какую группу должна попадать данная настройка
	Если НЕ ЭтоГруппа Тогда
		Родитель = НайтиСоздатьГруппуНастроек(ТипОбъектОснование);
		
		Попытка
			//На БСП - фиксируем пользователя системы
			Автор = Пользователи.ТекущийПользователь().Наименование;
		Исключение
			//Не на БСП - пустая строка
			//@skip-warning
			Автор = "";
		КонецПопытки;
		
		ДатаРедактирования = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Активно = Истина;
	ДействиеСПодчиненнымиОбъектами 	= Перечисления.асд_ДействияСПодчиненнымиОбъектами.ОтменитьПроведениеДерева;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Возвращает группу в которую должна попасть настройка
// 
//Параметры:
// 	ТипДокумента - Строка - Описание
//Возвращаемое значение:
// 	СправочникСсылка.асд_Настройки - Описание
Функция НайтиСоздатьГруппуНастроек(ТипОбъект)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|   Ссылка
	|ИЗ
	|	Справочник.асд_Правила
	|ГДЕ
	|	ПометкаУдаления = Ложь
	|	И ЭтоГруппа = Истина
	|	И ТипОбъектОснование = &ТипОбъект
	|";
	
	Запрос.УстановитьПараметр("ТипОбъект", ТипОбъект);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли; 
	
	//Если группу не нашли, создаем новую
	ГруппаНастроек = Справочники.асд_Правила.СоздатьГруппу();
	ГруппаНастроек.ТипОбъектОснование = ТипОбъект;
	
	МассивСтрок = СтрРазделить(ТипОбъект, ".");
	Если МассивСтрок[0] = "Документ" Тогда
		ГруппаНастроек.Наименование = "Документ: " + Метаданные.Документы[МассивСтрок[1]].Синоним;
	ИначеЕсли МассивСтрок[0] = "Справочник" Тогда 	
		ГруппаНастроек.Наименование = "Справочник: " + Метаданные.Справочники[МассивСтрок[1]].Синоним;
	Иначе	
		ВызватьИсключение НСтр("ru = 'Неустановленный тип объекта'");
	КонецЕсли; 
	
	ГруппаНастроек.Записать();
	
	Возврат ГруппаНастроек.Ссылка;
	
КонецФункции

//Производит контроль настроек АСД
Функция КонтрольПравилаАСД() Экспорт
	
	КонтрольПройден = Истина;
	
	Если НЕ Справочники.асд_Правила.ПроверитьКорректностьНастройкиОтбора(ЭтотОбъект) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Ошибка при проверке настроек отбора'"));
		КонтрольПройден = Ложь;
	КонецЕсли; 
	
	Если НЕ Справочники.асд_Правила.ПроверитьКорректностьНастройкиЗаполнения(ЭтотОбъект) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Ошибка при проверке настроек заполнения'"));
		КонтрольПройден = Ложь;
	КонецЕсли; 
	
	Возврат КонтрольПройден;
	
КонецФункции

#КонецОбласти

#КонецЕсли

