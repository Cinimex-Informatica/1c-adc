#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСписокВыбораОбъектамиПоМетаданным(СписокВыбора, Знач ВидОбъекта) Экспорт
		
	СписокВыбора.Очистить();
	
	Если ВидОбъекта = "Справочник" Тогда
		ВидОбъекта = "Справочники";
	ИначеЕсли ВидОбъекта = "Документ" Тогда
		ВидОбъекта = "Документы";
	Иначе	
		Возврат;
	КонецЕсли; 
	
	Для каждого Объект Из Метаданные[ВидОбъекта] Цикл
		СписокВыбора.Добавить(Объект.Имя, Объект.Синоним);
	КонецЦикла; 
	
	СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораВидовОбъектов(СписокВыбора) Экспорт
	
	СписокВыбора.Добавить("Справочник");
	СписокВыбора.Добавить("Документ");
	
КонецПроцедуры

Функция ПроверитьКорректностьНастройкиОтбора(Правило) Экспорт
	
	КонтрольПройден = Истина;
	
	Если ЗначениеЗаполнено(Правило.ТипОбъектОснование) Тогда
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Правило.ТипОбъектОснование);
		ОбъектОснование = МенеджерОбъекта.ПустаяСсылка();
		
		Попытка
			асд_ОсновнойМодульСервер.НужнаОбработкаАСД(ОбъектОснование, Правило);
		Исключение
			КонтрольПройден = Ложь;
		КонецПопытки;
		
	Иначе
		КонтрольПройден = Ложь;
	КонецЕсли;
	
	Возврат КонтрольПройден;
	
КонецФункции
 
Функция ПроверитьКорректностьНастройкиЗаполнения(Правило) Экспорт
	
	КонтрольПройден = Истина;
	
	Если ЗначениеЗаполнено(Правило.ТипОбъектОснование) И ЗначениеЗаполнено(Правило.ТипОбъектРезультат) Тогда
		
		СтруктураНастроек = Правило.НастройкиАСД.Получить();
		
		Если СтруктураНастроек <> Неопределено Тогда
			
			МассивРеквизитов = Неопределено;
			СтруктураНастроек.Свойство("МассивРеквизитов", МассивРеквизитов);
			
			Если МассивРеквизитов <> Неопределено Тогда
				Для каждого СтрокаРеквизита Из МассивРеквизитов Цикл
					Если НЕ ЗначениеЗаполнено(СтрокаРеквизита.Реквизит) Тогда
						КонтрольПройден = Ложь;
					КонецЕсли; 
				КонецЦикла;	
			КонецЕсли;
			
			Если СтруктураНастроек.Свойство("ПрограммныйКод") = Истина Тогда
				
				НачатьТранзакцию();
				МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Правило.ТипОбъектОснование);
				
				Если Лев(Правило.ТипОбъектОснование, 9) = "Документ." Тогда
					//Нужно для запуска Выполнить(...)
					//@skip-warning
					ОбъектОснование = МенеджерОбъекта.СоздатьДокумент();
				ИначеЕсли Лев(Правило.ТипОбъектОснование, 11) = "Справочник." Тогда	
					ОбъектОснование = МенеджерОбъекта.СоздатьЭлемент();
				Иначе	
					ВызватьИсключение нСтр("ru = 'Не установленный тип объекта основания'");
				КонецЕсли; 
				
				//Нужно для запуска Выполнить(...)
				//@skip-warning
				НайденныеСсылки	 = Новый Массив;
				//Нужно для запуска Выполнить(...)
				//@skip-warning
				МассивОбъектов	 = Новый Массив;
				
				Попытка
					Выполнить(СтруктураНастроек.ПрограммныйКод);
				Исключение
					ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
					КонтрольПройден = Ложь;
				КонецПопытки;
				
				ОтменитьТранзакцию();
				
			КонецЕсли;
		КонецЕсли;
	Иначе
		КонтрольПройден = Ложь;
	КонецЕсли; 
	
	Возврат КонтрольПройден;
	
КонецФункции

#КонецОбласти

#КонецЕсли
